/*
    dFSM Language
*/

dFlow:
    (
    'entities'
        entities*=TrainableEntity
    'end'
    )?
    (
    'synonyms'
        synonyms*=Synonym
    'end'
    )?
    (
    'intents'
        intents*=Intent
    'end'
    )?
;

// ENTITIY --------------------------------------------------------------
Entity: TrainableEntity | PredefinedEntity;

TrainableEntity:
    'Entity' name=ID
        words+=Word[',']
    'end'
;

PredefinedEntity:
    'PERSON'        |
    'NORP'          |
    'FAC'           |
    'ORG'           |
    'GPE'           |
    'LOC'           |
    'PRODUCT'       |
    'EVENT'         |
    'WORK_OF_ART'   |
    'LAW'           |
    'LANGUAGE'      |
    'DATE'          |
    'TIME'          |
    'PERCENT'       |
    'MONEY'         |
    'QUANTITY'      |
    'ORDINAL'       |
    'CARDINAL'
;

// ----------------------------------------------------------------------

Synonym:
    'Synonym' name=ID
        words+=Word[',']
    'end'
;

// INTENT
Intent:
    'Intent' name=ID
        phrases*=IntentPhraseComplex[',']
    'end'
;

IntentPhraseComplex: (IntentPhrase)*;

IntentPhrase:
    (
        phrase?=STRING
        ('TE:' refTrainable?=[TrainableEntity|FQN])?
        ('S:' refSynonym?=[Synonym|FQN])?
        ('PE:' refPredefined?=[PredefinedEntity|FQN] ('[' refPreValues*=STRING[','] ']')?)?
    )#
;

Phrase:
    words*=Word
;

// ----------------------------------------------------------------------

// EVENTS -------------------------------------------------------------->

Event: OnIntentEvent;

OnIntentEvent:
    'OnIntent' '(' intent=[Intent|FQN] ')' '->' action=[Action|FQN]
;

OnActionCompleted:
    'OnActionCompleted' '(' actionRef=[Action|FQN] ')'
;
// ----------------------------------------------------------------------


BotResponse: Action | ActiveForm;

ActiveForm:
    'ActiveForm' name=ID
        (
            'parameters'
                params+=ActiveFormParameter
            'end'
        )?
    'end'
;

ActiveFormParameter:
    name=ID ':' type=ParameterType '=' source=ParameterSource
;

ParameterSource: HRIParamResource;

HRIParamResource:
    'HRI' '(' ask_slot=STRING ')'
;

ParameterType: 'int' | 'float' | 'str' | 'bool';
ParameterDefault: STRING | INT | FLOAT | BOOL;



// ACTIONS ------------------------------------------------------------->

Action: SpeakAction | FireEventAction | HTTPCallAction;

SpeakAction:
    'Speak' '(' text=STRING ')'
;

FireEventAction:
    'FireEvent' '(' uri=STRING ',' msg=STRING ')'
;

HTTPCallAction:
    'RESTCall' '('
        host=STRING ','
        port=INT ','
        path=STRING ','
        '[' query_params*=STRING[','] ']' ','
        '[' path_params*=STRING[','] ']' ','
        '[' body_params*=STRING[','] ']' ','
    ')'
;

// ----------------------------------------------------------------------
Word:
    /[-\w]*\b/
;


TextType:
  STRING|/\w*\b/
;

// === Fully Qualified Names Rule ===
FQN: ID('.'ID)*;

// special rule for comments.
Comment:
    /\/\/.*$/
;
