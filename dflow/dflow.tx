/*
 *            DO WHAT THE FUCK YOU WANT TO PUBLIC LICENSE
 *                    Version 2, December 2004
 *
 * Copyright (C) 2022 Panayiotou, Konstantinos <klpanagi@gmail.com>
 * Author: Panayiotou, Konstantinos <klpanagi@gmail.com>
 *
 * Everyone is permitted to copy and distribute verbatim or modified
 * copies of this license document, and changing it is allowed as long
 * as the name is changed.
 *
 *            DO WHAT THE FUCK YOU WANT TO PUBLIC LICENSE
 *   TERMS AND CONDITIONS FOR COPYING, DISTRIBUTION AND MODIFICATION
 *
 *  0. You just DO WHAT THE FUCK YOU WANT TO.
 */

/*
    dFlow Language
*/

dFlow:
    (
    'entities'
        entities*=TrainableEntity
    'end'
    )?
    (
    'synonyms'
        synonyms*=Synonym
    'end'
    )?
    (
    'triggers'
        triggers+=Trigger
    'end'
    )?
    (
    'dialogues'
        dialogues+=Dialogue
    'end'
    )?
;

// ENTITIY --------------------------------------------------------------
// ----------------------------------------------------------------------
Entity: TrainableEntity | PretrainedEntity;

TrainableEntity:
    'Entity' name=ID
        words+=Words[',']
    'end'
;

PretrainedEntity:
    'PERSON'        |
    'NORP'          |
    'FAC'           |
    'ORG'           |
    'GPE'           |
    'LOC'           |
    'PRODUCT'       |
    'EVENT'         |
    'WORK_OF_ART'   |
    'LAW'           |
    'LANGUAGE'      |
    'DATE'          |
    'TIME'          |
    'PERCENT'       |
    'MONEY'         |
    'QUANTITY'      |
    'ORDINAL'       |
    'CARDINAL'
;
// ----------------------------------------------------------------------
// ----------------------------------------------------------------------

Synonym:
    'Synonym' name=ID
        words+=Words[',']
    'end'
;

// TRIGGER --------------------------------------------------------------
// ----------------------------------------------------------------------
Trigger: Intent | Event;

Intent:
    'Intent' name=ID
        phrases+=IntentPhraseComplex[',']
    'end'
;

Event:
    'Event' name=ID
        uri=STRING
    'end'
;

IntentPhraseComplex: phrases+=IntentPhrase;

IntentPhrase: IntentPhraseStr | IntentPhraseTE | IntentPhraseSynonym | IntentPhrasePE;

IntentPhraseStr: STRING;

IntentPhraseTE: 'TE:' trainable=[TrainableEntity|FQN];

IntentPhrasePE: 'PE:' pretrained=[PretrainedEntity|FQN] ('[' refPreValues*=STRING[','] ']')?;

IntentPhraseSynonym: 'S:' synonym=[Synonym|FQN];
// ----------------------------------------------------------------------
// ----------------------------------------------------------------------

// DIALOGUE -------------------------------------------------------------
// ----------------------------------------------------------------------
Dialogue:
    'Dialogue' name=ID
        'on:' onTrigger=[Trigger|FQN|^triggers]
        'responses:' responses+=DialogueResponse[',']
    'end'
;

DialogueResponseDef:
    'response' (name=ID)? response=DialogueResponse
;

DialogueResponse: ActionGroup | SlotForm;

SlotForm:
    'SlotForm' name=ID
        params+=FormParam
    'end'
;

FormParam:
    name=ID ':' type=ParameterTypeDef '=' source=FormParamSource
;

FormParamRef: param=[FormParam|FQN|^dialogues*.responses.params];

FormParamSource: HRIParamSource | EServiceParamSource;

HRIParamSource:
    'HRI' '(' askSlot=STRING (',' '['extract+=ExtractionSource[','] ']')? ')'
;

EServiceParamSource: EServiceHTTP;

EServiceHTTP:
    'EServiceHTTP' '('
        (verb=HTTPVerb ',')
        (host=STRING ',')
        (port=INT ',')
        (path=STRING ',')
        (
        ('query=' '[' query_params*=EServiceParam[','] ']' ',')?
        ('path=' '[' path_params*=EServiceParam[','] ']' ',')?
        ('body=' '[' body_params*=EServiceParam[','] ']' ',')?
        )#
    ')'
;

EServiceParam: name=ID '=' value=ParameterValue;

ParameterValue: INT | FLOAT | STRING | BOOL | List | Dict | FormParamRef;
ParameterTypeDef: 'int' | 'float' | 'str' | 'bool' | 'list' | 'dict';

ExtractionSource: ExtractFromIntent | ExtractFromEntity;

ExtractFromIntent: intent=[Trigger] ':' value=ParameterValue;
ExtractFromEntity: entity=[Entity];

// ----------------------------------------------------------------------
// ----------------------------------------------------------------------

// ACTIONS --------------------------------------------------------------
// ----------------------------------------------------------------------
ActionGroup:
    'ActionGroup' name=ID
        actions+=ActionTypes
    'end'
;


ActionTypes: SpeakAction | FireEventAction | RESTCallAction;

SpeakAction:
    'Speak' '(' text+=Text ')'
;

FireEventAction:
    'FireEvent' '(' uri=STRING ',' msg=Text ')'
;

RESTCallAction: EServiceHTTP;
// ----------------------------------------------------------------------
// ----------------------------------------------------------------------

// GENERAL --------------------------------------------------------------
// ----------------------------------------------------------------------

HTTPVerb: 'GET'|'POST'|'PUT';

DictItem:
    name=ID ':' value=DictTypes
;

DictTypes:
    NUMBER | STRING | BOOL | Dict | List | FormParamRef
;

Dict:
    '{' items*=DictItem[','] '}'
;

List:
    '[' items*=ListElements[','] ']'
;

ListElements:
    NUMBER | STRING | BOOL | List | Dict | FormParamRef
;

Words:
    /[-\w ]*\b/
;

Text: TextStr | FormParamRef;

TextStr: STRING;
TextEntity: entity=[Entity|FQN|^entities];
// ----------------------------------------------------------------------
// ----------------------------------------------------------------------

// === Fully Qualified Names Rule ===
FQN: ID('.'ID)*;

// special rule for comments.
Comment:
    /\/\/.*$/
;
// ----------------------------------------------------------------------
